//---------------------------Mouse Functions--------------------------
//As you've probably guessed, this .dic file has to do with mouse functions. This includes the petting feature and what happens when you double click your ghost.
//As a note, this .dic file isn't strictly necessary - I'm pretty sure you can just clear the file and it won't hurt your ghost any, it'll just make it so you can't pet or double click them. And if you're interested in only petting or double clicking one character, all you have to do is delete the chunks of code for the other character you want to be uninvolved. However, I'm assuming most people making ghosts want to make their ghosts pettable and clickable, so this will cover the messages for both.

//Make sure to read the walkthrough page (http://www.ashido.com/ukagaka/walkthrough.html) for more details about how to properly format ghost dialogue!
//I will reiterate that you should be editing these in Notepad++, and in particular, you should set the Language to C (or Java I guess) to properly highlight all the text. It will make things A LOT EASIER for you, trust me! It will also help you keep track of your brackets in case you forget some. If you click on a line with a bracket in Notepad++, it should highlight where the other bracket ends, or turn purple if there's no finishing bracket (which you should definitely fix!)
//It will also highlight my commented lines in green, so you'll know where they are. Any line beginning with // is a commented line that the code will not read. It's just for your reference!


//--OnMouseMove
//This is a bunch of code determining who the mouse is over. Don't touch any of it.

OnMouseMove
 {
  	if nowmode != "QUIZ"
 	{
		//---- reference3 determines the character in question
		if reference3 == 0 //if the character is 0, aka the main character
		{
			//---- it will run the mouse functions for that character
			MouseMoveSakura
		}
		else //if the character isn't 0, then it must be the side character.
		{
			//---- this will run the mouse functions for the side character.
			MouseMoveKero
		}
	}
 }
 
 //*********************Advanced User Info*******************************
//These text boxes are meant for those who have an interest in expanding or using more complicated functions in their ghost. Also for my own reference in the future since I plan on using this template too, haha. If you have no interest in any of this and just want to keep things as simple as possible, feel free to skip these boxes.

//If you want to have more than two characters at any point, like for example how I added Charger with my Hunter/Smoker ghost, and you want to be able to interact with that character like you can with the others, you will have to add in that character to OnMouseMove above. You'll need to know the p[] you've assigned your third character to, which in Charger's case was 3. You can assign your third character any p you want, just be consistent about using the same p[] everytime. I touch on \p a bit more in the walkthrough.
//So anyway, as you can see above, OnMouseMove determines who the user is touching, so you'll just have to add in an elseif checking for reference3 == 3, or reference3 == 4, or whatever p number is you're using. More on elseif in the walkthrough and in bootend.dic. After that, you'll just need to copy and paste the other mouse functions below and change the name as you need (for example, MouseMoveCharger, MouseDoubleClickCharger). 

//Note though that the extra character will not be a hard-coded character like 0 and 1! You're basically tricking the program into thinking there's another character, and some functions in things like menu.dic and aitalk.dic won't work quite right if you put in a value other than 0 or 1, and you'll need to be careful about making sure to remove the third character when you're done. But luckily for you, this won't be a problem with any of the mouse functions as far as I'm aware, and you can fill them out for the third character just like you would for the two main ones.
//***********************************************************************


 //--MouseMoveSakura
 //The petting function for your main character. Remember how it was referred to as sakura in some of the descript.txt and install.txt files?
 
MouseMoveSakura
 {
	if reference4 != "" //ignore
 	{
 		stroke++ //if you move the mouse over the right areas, the stroke count will go up
 		if stroke >= 40 //if the stroke count is above a certain number, the petting dialogue will kick in. You can change this number to make the petting take longer or shorter if you want. FLELEs have it set to 50 for some reason, which makes it take longer than this.
		{
 			stroke = 0 //resetting the stroke count
			if reference4 == "Head" || reference4 == "Face" //do you remember the names of the areas you set up for your collisions in surfaces.txt? This is where they come into play! This is where you'd put in the name of your collision, such as head, face, horns, tail, ears, whatever. As mentioned in surfaces.txt, you can add as many collisions to a pose as you want, as long as you make sure to add them all in here so they can actually be used.
 			{
				CharlieLoveRate++
				if CharlieLoveRate >= 70
				{
					"\0\s[1]Ah, %(username)! Wassup?\s[25] Why all the loving, aha?\e"
					"\0\s[6]Awh, I love you too buddy!\e"
					"\0\s[5]You're so lovely, %(username)!\e"
					"\0\s[1]%(username)! I'd pet you back\s[4] if I could...\e"
					"\0\s[6]You're so sweet, %(username)..."
				}
				elseif CharlieLoveRate >= 25 && CharlieLoveRate < 70
				{
					"\0\s[1]You really love giving me pets, huh?\e"
					"\0\s[5]Hehe! I love it when you make a fuss of me like this!\e"
					"\0\s[6]Awh, thank you so much! <33\e"
					"\0\s[5]You're so nice, thankie! Hehe!\e"
					"\0\s[6]Petz timez!!\e"
					"\0\s[25]%(username)! Hehe!\e"
					"\0\s[6]Oooh~! Hello, %(username)! Thank youuu!\e"
					"\0\s[1]Gosh, %(username)...\e"
					"\0\s[25]Hehe, thank you! You're too sweet...\e"
					"\0\s[5]I'm guessing you really like my fluffy hair, huh?\e"
				}
				elseif CharlieLoveRate >= 0 && CharlieLoveRate < 25
				{
					"\0\s[5]My hair is kinda fluffy, \w3isn't it?\e"
					"\0\s[5]Awh! Hehe, hello there~!\e"
					"\0\s[5]Thank you! Hehe...\e"
					"\0\s[6]Awh! Thank you!\e"
					"\0\s[0]Oh? Pets?"
					//Replace these with whatever petting responses you like. You can also add more dialogue here if you want, or cut them down. Though removing them entirely seems a bit counter-intuitive to all that work we've been doing.
				}
				elseif CharlieLoveRate < 0
				{
					"\0\s[2]Oh!\s[3] uhm... uh\s[4] thanks...?\e"
					"\0\s[9]I don't think we are close enough for that sort of attention, %(username).\e"
					"\0\s[4]Thanks, I guess? But, I'd really prefer if you didn't...\e"
					"\0\s[9]...\e"
					"\0\s[4].\w8\w8.\w8\s[9].\e"
				}
				else
				{
					"\0\s[0]Oh? Pets?"
				}
				
				//Replace these with whatever petting responses you like. You can also add more dialogue here if you want, or cut them down. Though removing them entirely seems a bit counter-intuitive to all that work we've been doing.
 			}
 			
			elseif reference4 == "Stomach"
			{
				if CharlieLoveRate >= 0
				{
				CharlieLoveRate++
				--
				"\0\s[23]Ahh! \w3That tickles!!\e"
				"\0\s[23]Who brought on the tickle monster? ahah!\e"
				"\0\s[23]Ahhh!\w3 Stop!\w3 I'm really t-ticklish! \w3ahaha!\e"
				"\0\s[23]W-wait! \w3Stop! \w3ahaha!\w3 I'm gonna pee!\e"
				}
				else
				{
				CharlieLoveRate -=2
				"\0\s[7]Hey no, don't tickle me.\e"
				"\0\s[7]What made you think trying to do that was a good idea? No, thank you.\e"
				"\0\s[9]No, go away. I don't want you touching my stomach.\e"
				"\0\s[9]Don't do that.\e"
				}
				
			}
			elseif reference4 == "LeftHand" || reference4 == "RightHand"
			{
			if CharlieLoveRate >= 0
			{
				"\0\s[0]Ah, would you like to hold my hand?\e"
				"\0\s[0]Oh, woah\w4, hello! You okay?\e"
				"\0\s[0]Henry always says my hands are quite soft. I don't agree, aha.\e"
				"\0\s[0]I'm not sure if I could hold your hand... Maybe I could hold the cursor? Hmm...\e"
			}
			else
			{
				"\0\s[4]Huh? What do you want my hand for...?\e"
				"\0\s[3]What?\s[4] Sorry, I don't want to hold your hand...\e"
				"\0\s[2]Hey!\s[4] Uhm, no hand holding for me...\e"
			}
				
			}
			elseif reference4 == "Feet"
			{
				CharlieLoveRate = CharlieLoveRate - 1
				--
				"\0\s[9]Please \w4don't touch my feet.\e"
				"\0\s[9]Get away from my feet, \w4please.\e"
				"\0\s[9]Wh- \w4Stop touching my feet! Please.\e"
				"\0\s[9]Can you not touch my feet? Thank you.\e"
				"\0\s[9]Don't touch my feet, please. Thanks.\e"
			}
 		}
	}
 	else
	{
 		// If the mouse isn't over the ghost, it'll zero out the stroke value.
		stroke = 0
 	}
}

 //---- MouseMoveKero -----------------------------------------------------------------
 //MouseMoveKero is exactly like the above, except set up for your second character. 
 

//---- OnMouseDoubleClick -------------------------------------------------------
//This is set up much like OnMouseMove above, except this is checking to see if the user is double clicking on the ghost. You also don't need to touch any of this. 
//Advanced users adding a third character, the same principles from OnMouseMove apply here as well! Make sure to add an elseif with the right number for your third character.

OnMouseDoubleClick
 {
	//---- like with the OnMouseMove function above, this determines who the user is clicking on.
	
 	if reference3 == 0 //if the character is 0, aka your main character
	{
		MouseDoubleClickSakura
 	}
	else
 	{
 		MouseDoubleClickKero
	}
 }

 //---- MouseDoubleClickSakura ----------------------------------------------------------
 //Like the petting animation above, this is the reactions they have to being double clicked. These also rely on the collisions you set up in surfaces.txt! This set is specifically for the main character, as indicated by the Sakura in the name again.
 
 //*********************Advanced User Info*******************************
 //As a note, double clicking on a certain area of your ghost doesn't have to be the user hitting them. You could instead set up a collision for a piece of clothing, and then have the character talk about that bit of clothing when it's double clicked, for example. One of Smoker's double clicks gives her a high-five when she's in a certain pose. Maybe double clicking is patting them, or pointing behind them, or tapping them, or any number of things. You can be creative here with how they respond depending on what areas you've set up as collisions!
 
 //If you don't want your character to be hittable at all, you can just delete everything in MouseDoubleClickSakura except OpenSakuraMenu. So it'd look like
 
 //MouseDoubleClickSakura
 //{
 //OpenSakuraMenu
 //}
 
 //That's all there is to it. This method also applies to the secondary character, Kero, below this set.
 //***********************************************************************
 
MouseDoubleClickSakura
 {
 	if reference4 == "Head" || reference4 == "Face" //here, this is checking to see if the user is double clicking on either the character's head OR their face, which for our purposes means they're hitting the ghost. More about operators at the walkthrough.
	{
		HitRate ++
		CharlieLoveRate -= 1
		if CharlieLoveRate > 50
		{
			CharlieLoveRate -= 4
			"\0\s[21]Ouch!! %(username)...?\w8\w8\nD-Did...\w8 Did I do something wrong..?\s[22] I'm really sorry...\e"
			"\0\s[2]Aah\s[21]! Wh-\s[22]%(username)...\e"
		}
		else
		{
			"\0\s[21]Ouch! What was that for?!\e"
			"\0\s[21]Hey, I didn't deserve that!\e"
			"\0\s[21]Ouchie, ouch, ouch! No need for the violence... geez...\e"
			"\0\s[21]Ahh! Ugh.. Ouch...\e"
			"\0\s[21]Hey! What did you do that for?\e"
			"\0\s[21]O-ouch! Did... Did I upset you...?\e"
		}
		//Replace this dialogue with appropriate dialogue as you need. You can add more or cut it down if you want, even down to nothing if you want no response from your ghost at all when those areas are double clicked.
 	}
	elseif reference4 == "Stomach"
	{
		HitRate += 2
		CharlieLoveRate -= 2
		if CharlieLoveRate > 50
		{
			CharlieLoveRate -= 4
			"\0\s[20]Ouch!! %(username)...?\w8\w8\nD-Did...\w8 Did I do something wrong..? I'm really sorry...\e"
			"\0\s[20]Aah! Wh-%(username)...?? Th-that really hurt...\e"
		}
		else
		{
			"\0\s[20]Ouch!! That hurt a lot, why would you punch me?!\e"
			"\0\s[20]Ugh- g-good punch! Please, don't try that on me next time though...\e"
			"\0\s[20]Ouchie ouch... I can't believe you've done this...\e"
			"\0\s[20]Ugh... Huff... I think you winded me...\e"
			"\0\s[20]I-if you wanted to open the menu... Next time, press my chest pocket-\e"
			"\0\s[20]Ack- Gosh... Owie... Can you not do that please...? It hurts..."
		}
		
	}
	else
 	{
		//Otherwise, if the user doubleclicks somewhere else on the character, they will open their menu as detailed in menu.dic.
 		OpenSakuraMenu
	}
 }

 
 //---- MouseDoubleClickKero -----------------------------------------------------------
 //This is just the same as MouseDoubleClickSakura above, except for your secondary character. The same principles apply, including how to make your character unhittable.


 //*********************Advanced User Info*******************************
 //There are other mouse related functions you could set up much like the above, such as OnMouseDragStart, OnMouseDragEnd, OnMouseWheel, and others which correspond to various things you can do with your mouse. You can probably guess what those functions do just by their names - OnMouseWheel would be if someone spun their mouse wheel over a certain area, for example. If you're interested in these, I'd recommend doing some reading at the CROW-SSP Encyclopedia to get a better idea of them and settle down for some trial and error learning as you try to put them in.
 //***********************************************************************